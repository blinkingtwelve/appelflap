apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

buildscript {
    ext.androidx_navigation_version = "2.3.5"
    repositories { mavenCentral() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}


def getgitref() {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    proc.text.trim()
}


// Enable this if you're building GeckoView locally
/*
ext.topsrcdir = "/home/boer/documents/devschuur/mozilla-central"
apply from: "${topsrcdir}/substitute-local-geckoview.gradle"

repositories {
    maven {
        name "Local GeckoView Maven repository"
        url "${topobjdir}/gradle/build/mobile/android/geckoview/maven"
    }
}
*/

android {
    signingConfigs {
        demo {
            keyPassword ''
            storeFile file('../../appelflap-keystore.jks')
            storePassword ''
            keyAlias 'demo-apksign'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileSdk 29
    buildToolsVersion '31.0.0'
    defaultConfig {
        minSdkVersion 23  // Android 6
        targetSdkVersion 29 // < 23 allows manifest perms to be autogranted to system app
    }
    buildTypes {
        all {
            minifyEnabled false
            shrinkResources false
            archivesBaseName = "appelflap"
            resValue "string", "P2P_MONITOR_ENABLED", "false"
            buildConfigField "boolean", "FRONTEND_USES_INJECTIONLOCK", "true"
            buildConfigField "boolean", "WIFI_P2P_ENABLED", "false"

            // make sure these are in sync with any resConfigs
            buildConfigField "String", "DEFAULT_LANGUAGE", '"en"'
            buildConfigField "String[]", "ENABLED_LANGUAGES", 'new String[]{"en", "bi", "pis", "sm"}'
        }
        release {
            versionNameSuffix '-release'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        debug {
            versionNameSuffix '-debug'
            resValue "string", "SENTRY_DSN", ""
            resValue "string", "P2P_MONITOR_ENABLED", "true"
            isDefault true
        }
    }
    compileOptions {
        // geckoview needs 1.8 API
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        // remove bloatiest things brought in by org.xerial:sqlite-jdbc
        // on Android, we don't use JDBC.
        exclude 'org/sqlite/native/**'
    }

    flavorDimensions "abi"

    splits {
        abi {
            enable true
//            reset()
//                include 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
            universalApk true
        }
    }


    sourceSets {
        demo {
            setRoot 'src/demo'
        }
    }

    productFlavors {
        demo {
            signingConfig signingConfigs.demo
            dimension 'abi'

            // make sure these language related declarations are intra-consistent. use the `res/values/string` translation editor to add/fill in translations.
            resConfigs "en", "bi", "pis", "sm"

            applicationId "org.trivialpursuit.appelflap.demo"
            versionCode 100
            versionName '1.0.0'
            resValue "string", "WEBWRAP_TITLE", "Appelflap Demo"
            resValue "string", "SUPPORTAPP_TITLE", "Appelflap Demo Sidecar"

            buildConfigField "String", "WEBWRAP_URL", '"http://127.0.0.1:8020"'
            buildConfigField "String[]", "WEBWRAP_DOMAINS", 'new String[]{"127.0.0.1"}'
            resValue "string", "SENTRY_DSN", ""

            buildConfigField "String", "WEBPUSH_POLL_URL", '"http://127.0.0.1:8010/notifications/appelflap_letterbox"'
            buildConfigField "int", "WEBPUSH_POLL_INTERVAL_QTR", '0'  // in units of 15 minutes, 0 disables it
        }
    }
    ndkVersion '23.0.7599858'
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
        variant.resValue "string", "applicationId", variant.applicationId
        variant.resValue "string", "P2P_LOGFILEPROVIDER_AUTHORITY", variant.applicationId + ".logfileprovider"
        variant.resValue "string", "DOWNLOADS_FILEPROVIDER_AUTHORITY", variant.applicationId + ".downloadsfileprovider"
        variant.resValue "string", "TEMPFILE_FILEPROVIDER_AUTHORITY", variant.applicationId + ".tempfileprovider"
        variant.resValue "string", "APKUPGRADE_FILEPROVIDER_AUTHORITY", variant.applicationId + ".apkupgradefileprovider"

        variant.outputs.all { output ->
            output.outputFileName = output.outputFileName.replace(".apk", "-${getgitref()}.apk").replace("appelflap-", "")
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

    /*
       Geckoview library. We use a custom repo which carries some patches for GeckoView (see "mozilla-central-patches/README.md") that we need.
       You could switch to some other GeckoView, but that is going to crash your app unless you've applied the appropriate patches yourself.
       To use other GeckoView libraries, you'll need to switch the maven repo definitions.
       For that, see the example for locally built GeckoView at the top of this file, and the main build.gradle.
    */
    implementation "org.mozilla.geckoview:geckoview-default:89.0.20210616152425-20210616.165243-1"

    implementation 'io.sentry:sentry-android:4.3.0'
    implementation 'io.github.rybalkinsd:kohttp:0.12.0'
    implementation 'io.nayuki:qrcodegen:1.6.0'
    implementation "com.github.YarikSOffice:lingver:1.3.0"

    implementation project(path: ':eekhoorn')
    implementation project(path: ':koekoek')
    implementation project(path: ':kitchensink')
}
